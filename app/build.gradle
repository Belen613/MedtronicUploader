apply plugin: 'com.android.application'

def gitVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1
    }
}

def gitVersionName = { ->
    try {
        def stdout =  new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.nightscout.android"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode gitVersionCode()
        versionName gitVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

}

dependencies {
    compile files('libs/bugfender.jar')
    compile files('libs/logback-android-1.1.1-3.jar')
    compile files('libs/physicaloidlibrary.jar')
    compile files('libs/slf4j-api-1.7.2.jar')

}
